plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.9.6'
}

repositories {
    mavenCentral()
}

def allureVersion = "2.17.3",
    selenideVersion = "6.5.0",
    junitVersion = "5.8.2"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
            "org.aspectj:aspectjweaver:1.9.19",
            "com.codeborne:selenide:6.15.0",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "org.assertj:assertj-core:3.19.0",
            "org.slf4j:slf4j-simple:2.0.0",
            'io.rest-assured:rest-assured:4.4.0',
            "org.junit.jupiter:junit-jupiter:$junitVersion")
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task simpletest(type: Test) {
    useJUnitPlatform {
        includeTags 'simpleTests'
    }
    systemProperties(System.getProperties())
}

task uitest(type: Test){
    useJUnitPlatform {
        includeTags 'uiTests'
    }
    systemProperties(System.getProperties())
}
